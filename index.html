<!DOCTYPE html>	<!--表示文档类型为HTML文档-->
<html>
	<head><!--文档头部-->
		<!--使用UTF-8字符编码，无此句在某些浏览器会出现乱码-->
		<meta charset = "utf-8">
		<!--文档标题，会显示在网页标题栏-->
		<title>迷雾</title>

		<style>
			div{
				margin:0 auto;
				width: 1024px;
			}
		</style>

	</head>
	
	<body><!--文档主体-->
		<!--绘图区域的id，以及宽度和高度，用WebGL绘制的图形将显示在该canvas中-->
		<div>
		<canvas id="webgl" width="1024" height="600" style='position:absolute;z-index:0'>
		对不起，你的浏览器不支持HTML5的canvas元素！<!--出错时显示这句话-->
		</canvas>
		<canvas id="hud" width="600" height="300" style='position:absolute;z-index:1'>
		对不起，你的浏览器不支持HTML5的hud元素！<!--出错时显示这句话-->
		</canvas>
		</div>
		
		<!--以下为用到的shader程序和JavaScript程序，程序类型由type属性决定，
		顶点Shader程序和片元Shader程序有id，因为在JavaScript程序中需要对其
		进行访问，而JavaScript程序无id-->
		
		<!--顶点Shader程序-->
		<script id="vertex-shader" type="x-shader/x-vertex">
		const int LightNum = 3;		//光源数量

		attribute vec3 a_Position;	//顶点坐标
		attribute vec3 a_Normal;	//顶点法线
		
		attribute vec2 a_Texcoord;	//顶点纹理坐标
		varying vec2 v_Texcoord;	//片元纹理坐标

		varying vec3 v_fN;	//法线向量
		varying vec3 v_fE;	//观察向量
		varying vec3 v_fL[LightNum];	//光照向量
		varying float v_Dist;	//到聚光灯距离

		//雾化
		uniform vec4 u_eye;
		varying float v_eyeDist;

		uniform mat4 u_ModelView;	//模视矩阵
		uniform mat4 u_Projection;	//投影矩阵
		uniform mat3 u_NormalMat;	//法向变换矩阵

		uniform vec4 u_LightPosition[LightNum];	//光源位置

		uniform bool u_bOnlyTexture;	//是否仅贴图

		void main(){
			v_Texcoord = a_Texcoord;

			vec3 pos = (u_ModelView * vec4(a_Position, 1.0)).xyz;

			if(!u_bOnlyTexture){
				v_fE = normalize(-pos);
				v_fN = normalize(u_NormalMat * a_Normal);

				for(int i=0;i<LightNum;i++){
					if(u_LightPosition[i].w != 0.0) v_fL[i] = normalize(u_LightPosition[i].xyz - pos);
					else v_fL[i] = normalize(u_LightPosition[i].xyz);
				}
				
				v_Dist = distance(pos, u_LightPosition[2].xyz);

				//雾化
				v_eyeDist = distance(pos, u_eye.xyz);
			}

			gl_Position = u_Projection * vec4(pos, 1.0);	
		}
		</script>
		<!--片元Shader程序-->
		<script id="fragment-shader" type="x-shader/x-fragment">
		const int LightNum = 3;		//光源数量
		precision mediump float;	//浮点数精度为中等
		uniform vec3 u_Color;	 	//仅贴图时的明暗

		varying vec2 v_Texcoord;		//片元纹理坐标
		uniform sampler2D u_Sampler;	//纹理贴图

		varying vec3 v_fN;	//法线向量
		varying vec3 v_fE;	//观察向量
		varying vec3 v_fL[LightNum];	//光照向量
		varying float v_Dist;	//到聚光灯距离

		uniform float u_Shininess;	//高光系数
		//光源属性
        uniform vec3 u_AmbientProduct[LightNum];
		uniform vec3 u_DiffuseProduct[LightNum];
		uniform vec3 u_SpecularProduct[LightNum];

		uniform vec3 u_Emission;	//发射光属性
		uniform float u_Alpha;	//透明度

		uniform bool u_LightOn[LightNum]; //光源状态

		uniform vec3 u_SpotDirection;	//聚光灯照射方向
		uniform float u_SpotCutOff;		//聚光灯角度
		uniform float u_SpotExponent;	//聚光灯衰减系数

		uniform bool u_bToonShader; 	//是否为卡通渲染
		uniform bool u_bOnlyTexture;	//是否仅贴图

		//雾化
		varying float v_eyeDist;
		uniform vec3 u_FogColor;
		uniform vec2 u_FogDist;
		uniform bool u_bFog;
		
		float lerp(float from,float to, float t ) {
			t = max(0.0, min(1.0, t));
			return from + ( to - from ) * t;
		}

		void main(){
			if(u_bOnlyTexture){
				gl_FragColor = texture2D(u_Sampler, v_Texcoord) * vec4(u_Color,1.0);
				return;
			}

			vec3 color = vec3(0.0,0.0,0.0);
			vec3 specular = vec3(0.0,0.0,0.0);

			vec3 N = normalize(v_fN);
			vec3 E = normalize(v_fE);

			for(int i=0;i<LightNum;i++){
				if(!u_LightOn[i]) continue;

				vec3 L = normalize(v_fL[i]);
				vec3 H = normalize(L+E);

				float KSpot = 1.0;
				if(i==2){
					vec3 spotDir = -normalize(u_SpotDirection);
					float cutoff = radians(u_SpotCutOff);
					float c = dot(L, spotDir);
					if(c<cos(cutoff)) KSpot = 0.0;
					else {
						float d = 1.0 + 0.5 * v_Dist;
						KSpot = max(pow(c,u_SpotExponent),0.0)/d;
					}
				}

				vec3 ambient = u_AmbientProduct[i];

				float Kd;
				vec3 diffuse;
				if(u_bToonShader){
					Kd = max(dot(L,N),0.0);
					if(Kd>=0.05) Kd = 1.0;
					else if(Kd!=0.0) Kd = Kd / 0.05;
					diffuse = KSpot * Kd * u_DiffuseProduct[i];
				}
				else{
					Kd = max(dot(L,N),0.0);
					diffuse = KSpot * Kd * u_DiffuseProduct[i];
				}

				if(Kd!=0.0){
					float Ks;
					if(u_bToonShader){
						Ks = max(dot(N,H),0.0);
						float w = 0.005;
						float p = 0.01 * u_Shininess / 100.0;
						Ks = smoothstep(1.0-w,1.0+w,Ks + p);
					}
					else Ks = pow(max(dot(N,H),0.0),u_Shininess);
					specular = KSpot * Ks * u_SpecularProduct[i];
				}

				color += ambient + diffuse;
			}

			color += u_Emission;

			//雾化
			color = color * texture2D(u_Sampler, v_Texcoord).xyz + specular;
			float fogFactor = clamp((u_FogDist.y - v_eyeDist)/(u_FogDist.y - u_FogDist.x),0.0,1.0);
			if(u_bFog) color = mix(u_FogColor,color,fogFactor);
			gl_FragColor = vec4(color,1.0);

			//gl_FragColor = vec4(color,1.0) * texture2D(u_Sampler, v_Texcoord) + vec4(specular,1.0);
			gl_FragColor.a = u_Alpha;
		}
		</script>
		<!--OBJ片元Shader程序-->
		<script id="OBJfragment-shader" type="x-shader/x-fragment">
		const int LightNum = 3;		//光源数量
		precision mediump float;	//浮点数精度为中等

		varying vec2 v_Texcoord;		//片元纹理坐标
		uniform sampler2D u_Sampler;	//纹理贴图

		varying vec3 v_fN;	//法线向量
		varying vec3 v_fE;	//观察向量
		varying vec3 v_fL[LightNum];	//光照向量
		varying float v_Dist;	//到聚光灯距离

		// 光源属性
        uniform vec3 u_AmbientLight[LightNum];
		uniform vec3 u_DiffuseLight[LightNum];
		uniform vec3 u_SpecularLight[LightNum];

		uniform vec3 u_Ka;	//环境光反射系数
		uniform vec3 u_Kd;	//漫反射系数
		uniform vec3 u_Ks;	//镜面反射系数
		uniform vec3 u_Ke;	//发射系数
		uniform float u_Ns;	//高光系数
		uniform float u_d;	//透明度

		uniform bool u_LightOn[LightNum];	//光源状态

		uniform vec3 u_SpotDirection;	//聚光灯照射方向
		uniform float u_SpotCutOff;		//聚光灯角度
		uniform float u_SpotExponent;	//聚光灯衰减系数

		uniform bool u_bToonShader; 	//是否为卡通渲染

		//雾化
		varying float v_eyeDist;
		uniform vec3 u_FogColor;
		uniform vec2 u_FogDist;
		uniform bool u_bFog;
		
		float lerp(float from,float to, float t ) {
			t = max(0.0, min(1.0, t));
			return from + ( to - from ) * t;
		}
		
		void main(){
			vec3 color = vec3(0.0,0.0,0.0);
			vec3 specular = vec3(0.0,0.0,0.0);

			vec3 N = normalize(v_fN);
			vec3 E = normalize(v_fE);

			for(int i=0;i<LightNum;i++){
				if(!u_LightOn[i]) continue;

				vec3 L = normalize(v_fL[i]);
				vec3 H = normalize(L+E);

				float KSpot = 1.0;
				if(i==2){
					vec3 spotDir = -normalize(u_SpotDirection);
					float cutoff = radians(u_SpotCutOff);
					float c = dot(L, spotDir);
					if(c<cos(cutoff)) KSpot = 0.0;
					else {
						float d = 1.0 + 0.5 * v_Dist;
						KSpot = max(pow(c,u_SpotExponent),0.0)/d;
					}
				}

				vec3 ambient = u_AmbientLight[i] * u_Ka;
				
				float Fd;
				vec3 diffuse;
				if(u_bToonShader){
					Fd = max(dot(L,N),0.0);
					if(Fd>=0.05) Fd = 1.0;
					else if(Fd!=0.0) Fd = Fd / 0.05;
					diffuse = KSpot * Fd * u_DiffuseLight[i] * u_Kd;
				}
				else{
					Fd = max(dot(L,N),0.0);
					diffuse = KSpot * Fd * u_DiffuseLight[i] * u_Kd;
				}

				if(Fd!=0.0){
					float Fs;
					if(u_bToonShader){
						Fs = max(dot(N,H),0.0);
						float w = 0.005;
						float p = 0.01 * u_Ns / 100.0;
						Fs = smoothstep(1.0-w,1.0+w,Fs + p);
					}
					else Fs = pow(max(dot(N,H),0.0),u_Ns);
					specular = KSpot * Fs * u_SpecularLight[i] * u_Ks;
				}

				color += ambient + diffuse;
			}

			color += u_Ke;

			//雾化
			color = color * texture2D(u_Sampler, v_Texcoord).xyz + specular;
			float fogFactor = clamp((u_FogDist.y - v_eyeDist)/(u_FogDist.y - u_FogDist.x),0.0,1.0);
			if(u_bFog) color = mix(u_FogColor,color,fogFactor);
			gl_FragColor = vec4(color,1.0);

			//gl_FragColor = vec4(color,1.0) * texture2D(u_Sampler, v_Texcoord) + vec4(specular,1.0);
			gl_FragColor.a = u_d;
		}
		</script>
		<!--ToonShader轮廓线顶点Shader程序-->
		<script id="line-vertex-shader" type="x-shader/x-vertex">
		attribute vec3 a_Position;	//顶点坐标
		attribute vec3 a_Normal;	//顶点法线
		
		uniform mat4 u_ModelView;	//模视矩阵
		uniform mat4 u_Projection;	//投影矩阵
		uniform mat3 u_NormalMat;	//法向变换矩阵

		uniform float u_LineWidth;	//轮廓线宽度

		//雾化
		uniform vec4 u_eye;
		varying float v_eyeDist;

		void main(){
			vec3 pos = (u_ModelView * vec4(a_Position, 1.0)).xyz;
			vec3 normal = normalize(u_NormalMat * a_Normal);
			normal.z = -0.5;
			pos = pos + normalize(normal) * u_LineWidth;
			gl_Position = u_Projection * vec4(pos, 1.0);	
			
			//雾化
			v_eyeDist = distance(pos, u_eye.xyz);
		}
		</script>
		<!--ToonShader轮廓线片元Shader程序-->
		<script id="line-fragment-shader" type="x-shader/x-fragment">
		precision mediump float;	//浮点数精度为中等

		uniform vec3 u_LineColor;		//轮廓线颜色

		uniform float u_Alpha;			//透明度

		//雾化
		varying float v_eyeDist;
		uniform vec3 u_FogColor;
		uniform vec2 u_FogDist;
		uniform bool u_bFog;

		void main(){
			//雾化
			vec3 lineColor = u_LineColor;
			float fogFactor = clamp((u_FogDist.y - v_eyeDist)/(u_FogDist.y - u_FogDist.x),0.0,1.0);
			if(u_bFog) lineColor = mix(u_FogColor,lineColor,fogFactor);

			gl_FragColor = vec4(lineColor,u_Alpha);
		}
		</script>

		<!--下面是辅助我们进行程序编写的3个JavaScript程序-->
		<script type="text/javascript" src="./Common/webgl-utils.js"></script>
		<script type="text/javascript" src="./Common/initShaders.js"></script>
		<script type="text/javascript" src="./Common/MV.js"></script>
		<script type="text/javascript" src="./Common/ObjModel.js"></script>
		<!--主程序-->
		<script type="text/javascript" src="Scene.js"></script>
	</body>
</html>
